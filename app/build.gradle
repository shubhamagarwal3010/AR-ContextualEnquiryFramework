apply plugin: 'com.android.application'

def VUFORIA_SDK_DIR = '../'
def NATIVE_LIB_DIR = 'vuforia/lib'
def JAR_DIR = 'vuforia/java/Vuforia'

def vuforiaLicenceKey = System.getenv('VUFORIA_LICENCE_KEY')
def vuforiaAccessKey = System.getenv('VUFORIA_ACCESS_KEY')
def vuforiaSecretKey = System.getenv('VUFORIA_SECRET_KEY')

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir "$VUFORIA_SDK_DIR/$NATIVE_LIB_DIR/"
    }

    defaultConfig {
        applicationId "com.thoughtworks.onboarding"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }

    dataBinding {
        enabled = true
    }

    archivesBaseName = rootProject.projectDir.getName()

    // Ensure that Vuforia datasets placed in the assets folder do not get compressed as
    // this can significantly increase their loading time
    aaptOptions {
        noCompress 'dat'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "String", "VUFORIA_LICENCE_KEY", "\"$vuforiaLicenceKey\""
            buildConfigField "String", "VUFORIA_ACCESS_KEY", "\"$vuforiaAccessKey\""
            buildConfigField "String", "VUFORIA_SECRET_KEY", "\"$vuforiaSecretKey\""
        }
        debug {
            debuggable true
            minifyEnabled false

            buildConfigField "String", "VUFORIA_LICENCE_KEY", "\"$vuforiaLicenceKey\""
            buildConfigField "String", "VUFORIA_ACCESS_KEY", "\"$vuforiaAccessKey\""
            buildConfigField "String", "VUFORIA_SECRET_KEY", "\"$vuforiaSecretKey\""
        }
    }
}
repositories {
    mavenLocal()
    maven { url 'https://jitpack.io' }
}
dependencies {
    implementation files("$VUFORIA_SDK_DIR/$JAR_DIR/Vuforia.jar")
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
}
